# TODO: Look into inheritance so that the variables that represent the structure of the importer page
# eg: @checkboxSelector can be passed across differnet coffeescript files so we have
# one centralized source of those variables.

class NfgCsvImporter.AutoSaveNotification
  constructor: (@el) ->
    # Component Library
    @checkboxSelector = "input[type='checkbox']"
    @columnSelector = ".col-importer"
    @columns = @el.find @columnSelector
    @alertGrowlClass = "alert-growl"
    @thing = $(".alert-growl")

    # Elements
    @checkboxes = @columns.find @checkboxSelector
    @selects = @columns.find "select"
    # @columnLetters = @columns.find "[data-column]".data

    # Autosave HTML
    @html = """
            <div class='alert alert-success #{@alertGrowlClass}' role='alert'>
              Saving Settings <span id='additionalInfo'></span>
              <i class="fa fa-spinner fa-pulse fa-3x fa-fw"></i>
              <span class="sr-only">Saving...</span>
            </div>
           """

    # Actions
    # On click of a checkbox, launch autosave notification
    @checkboxes.on 'click', (event) =>
      interactiveElement = $(event.currentTarget)
      @displayAutoSave(interactiveElement)

    # On change of a select menu, launch autosave notification
    @selects.on 'change', (event) =>
      interactiveElement = $(event.currentTarget)
      @displayAutoSave(interactiveElement)


  # Display Autosave
  displayAutoSave: (interactiveElement) ->
    # Grab the column index letter so we can display it in the auto save notification
    columnIndex = interactiveElement.closest(@columnSelector).find(".col-index").data("column")

    # Make sure there isn't an autosave there
    if $(".alert-growl").length == 0
      $(@html).appendTo @el

      # Inject the column index additional content
      $('#additionalInfo').text columnIndex

      # Animation time!
      @animateAutoSave()


  # Make the magic happen :)
  animateAutoSave: =>

    # Convenience variables
    alertGrowlElement = $(".alert-growl")
    animationEnd = "animationend webkitAnimationEnd oAnimationEnd msAnimationEnd"
    growlStep1 = "growl-step1"
    growlStep2 = "growl-step2"
    growlStep3 = "growl-step3"

    # Step 1: Introduce Growl
    alertGrowlElement.addClass(growlStep1).on animationEnd, ->

      # Step 2: Announce Autosave
      $(@).addClass(growlStep2).on animationEnd, ->

        # Step 3: End autosave notification
        $(@).addClass(growlStep3).on animationEnd, ->

          # Step 4: remove the HTML
          $(@).remove()
          return false


$(document).on 'ready', ->
  el = $("#self_importer")
  return unless el.length > 0
  inst = new NfgCsvImporter.AutoSaveNotification el
